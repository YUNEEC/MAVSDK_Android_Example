apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

configurations {
    checkstyleClasspath
}

android {
    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures() {
        viewBinding = true
    }
    defaultConfig {
        applicationId "io.mavsdk.androidclient"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task checkstyle(type: Checkstyle) {
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = project.files()
    checkstyleClasspath = project.configurations.checkstyleClasspath

    maxWarnings 0

    reports {
        xml.enabled true
        xml.destination new File("$project.buildDir/reports/checkstyle/checkstyle_report.xml")

        html.enabled true
        html.destination new File("$project.buildDir/reports/checkstyle/checkstyle_report.html")
    }
}

build.dependsOn 'checkstyle'

dependencies {
    implementation fileTree(dir: 'libs', include: ['.jar', '.so'])
    implementation 'com.google.android.gms:play-services-location:19.0.1'
    checkstyleClasspath 'com.puppycrawl.tools:checkstyle:8.17'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.github.tony19:logback-android:2.0.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.9.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.2.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-telemetry:6.1.0'
    implementation 'io.mavsdk:mavsdk:1.2.0'
    implementation 'io.mavsdk:mavsdk-server:1.2.1'
    implementation 'org.slf4j:slf4j-api:1.7.35'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    def lifecycle_version = "2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'androidx.activity:activity-ktx:1.7.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.6'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'tv.danmaku.ijk.media:ijkplayer-java:0.8.8'
    implementation 'tv.danmaku.ijk.media:ijkplayer-exo:0.8.8'
}
repositories {
    mavenCentral()
}
